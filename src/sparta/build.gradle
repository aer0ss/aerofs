//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

archivesBaseName = 'aerofs-sparta'

test {
    // unit tests fail if we don't fork due to bad isolation
    forkEvery = 1
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            // Include SP's flyway db migration scripts
            srcDir '../spsv/resources/sp'
            include 'db/**'
            srcDir 'resources'
            include 'logback.xml'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

evaluationDependsOn(':src/libservlet')
evaluationDependsOn(':src/bifrost')

dependencies {
    compile 'org.flywaydb:flyway-core:2.2.1'
    compile 'org.apache.tomcat:tomcat-jdbc:7.0.50'
    compile project(':src/restless')
    compile project(':src/rest-api')
    compile project(':src/libservlet')
    compile project(':src/oauth-verify')
    // FIXME: libservlet deps on verkehr as:
    //   compile project(path: ':src/verkehr', configuration: 'restClient')
    // but for some reason, the verkehr rest client's dep on jackson-databind doesn't get propagated,
    // so I'm adding it explicitly here to make things work.
    runtime 'com.fasterxml.jackson.core:jackson-databind:2.2.1'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.1.4'
    testCompile project(':src/libservlet').sourceSets.test.runtimeClasspath
    testCompile project(':src/bifrost').sourceSets.test.runtimeClasspath
}

jar {
    manifest.attributes(
        'Main-Class': 'com.aerofs.sp.sparta.Sparta',
        'Class-Path': configurations.runtime.files.collect { it.name }.join(' '),
    )
}
