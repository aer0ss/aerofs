//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

archivesBaseName = 'aero-zephyr'

sourceSets {
    main {
        java {
            srcDir 'src'
            exclude 'com/aerofs/zephyr/client/**' 
        }
    }
    client {
        java {
            srcDirs 'src', 'gen'
            exclude 'com/aerofs/zephyr/server/**' 
        }
    }
}

configurations {
    client.extendsFrom clientCompile
}

dependencies {
    compile project(':src/lib')
    clientCompile project(':src/lib')
}

task clientJar(type: Jar) {
    baseName 'aero-zephyr-client'
    from sourceSets.client.output
}

artifacts {
    client clientJar
    archives clientJar
}

jar {
    manifest.attributes(
        'Main-Class': 'com.aerofs.zephyr.server.Zephyr',
        'Class-Path': configurations.runtime.files.collect { it.name }.join(' '),
    )
}
