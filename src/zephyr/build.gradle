//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

base {
    archivesName = 'aero-zephyr'
}

sourceSets {
    main {
        java {
            srcDir 'src'
            exclude 'com/aerofs/zephyr/client/**' 
        }
    }
    client {
        java {
            srcDirs 'src', 'gen'
            exclude 'com/aerofs/zephyr/server/**' 
        }
    }
}

configurations {
    client.extendsFrom clientImplementation
}

dependencies {
    implementation project(':src:lib')
    clientImplementation project(':src:lib')
}

task clientJar(type: Jar) {
    archiveBaseName = 'aero-zephyr-client'
    from sourceSets.client.output
}

artifacts {
    client clientJar
    archives clientJar
}

jar {
    manifest.attributes(
        'Main-Class': 'com.aerofs.zephyr.server.Zephyr',
        'Class-Path': configurations.runtimeClasspath.files.collect { it.name }.join(' '),
    )
}
