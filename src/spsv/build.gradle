//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

archivesBaseName = 'aerofs-sp'

test {
    // unit tests fail if we don't fork due to bad isolation
    forkEvery = 1
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

evaluationDependsOn(':src/libservlet')

dependencies {
    compile 'com.googlecode.json-simple:json-simple:1.1'
    compile 'commons-codec:commons-codec:1.6'
    compile 'org.mortbay.jetty:jetty:6.1.14'
    compile name:'dyuproject-util-1.1'
    compile project(':src/libservlet')
    // FIXME: libservlet deps on verkehr as:
    //   compile project(path: ':src/verkehr', configuration: 'restClient')
    // but for some reason, the verkehr rest client's dep on jackson-databind doesn't get propagated,
    // so I'm adding it explicitly here to make things work.
    runtime 'com.fasterxml.jackson.core:jackson-databind:2.2.1'
    testCompile name:'rest-assured-1.8.1'
    testCompile restAssuredDependencies
    testCompile project(':src/libservlet').sourceSets.test.runtimeClasspath
}

jar {
    manifest.attributes(
        'Class-Path': configurations.runtime.files.collect { it.name }.join(' '),
    )
}
