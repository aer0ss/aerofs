apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.7

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile project(':baseline')
    compile project(':polaris-api')
    compile 'org.flywaydb:flyway-core:3.0'
    runtime 'mysql:mysql-connector-java:5.1.32'
    testCompile project(':baseline-test')
    testCompile 'junit:junit:4.11'
    testCompile 'com.jayway.restassured:rest-assured:2.3.+'
    testCompile "org.mockito:mockito-core:1.10.+"
    testRuntime 'com.h2database:h2:1.4.181'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.aerofs.polaris.Polaris',
                'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

task dist(type: Zip, description: "Assembles a zipfile containing everything required to run a Polaris instance", dependsOn: jar) {
    classifier 'dist'
    from configurations.runtime
    from jar
    from 'src/dist'
    into "polaris-0.0"
}

artifacts {
    archives dist
}
