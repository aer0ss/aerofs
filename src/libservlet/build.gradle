//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

archivesBaseName = 'aerofs-libservlet'

sourceSets {
    main {
        java {
            srcDirs 'src', 'gen'
        }
    }
    testutil {
        java {
            srcDir 'test'
            include 'com/aerofs/servlets/MockSession.java'
            include 'com/aerofs/servlets/SecUtilHelper.java'
            include 'com/aerofs/servlets/lib/db/*.java'
            exclude 'com/aerofs/servlets/lib/db/Test*.java'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java {
            srcDir 'test'
            compileClasspath += testutil.output
            runtimeClasspath += testutil.output
        }
    }
}

configurations {
    testutilCompile.extendsFrom compile
    testutilRuntime.extendsFrom testutilCompile

    testutil.extendsFrom testutilCompile

    testCompile.extendsFrom testutilCompile
    testRuntime.extendsFrom testCompile, testutilRuntime
}

dependencies {
    compile 'org.flywaydb:flyway-core:3.2.+'
    compile 'com.lambdaworks:scrypt:1.4.+'
    compile 'javax.mail:mail:1.4.2'
    compile 'javax.servlet:servlet-api:2.5'
    compile 'mysql:mysql-connector-java:5.1.+'
    compile 'org.bouncycastle:bcprov-jdk15on:1.50+'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.50+'
    compile 'org.apache.tomcat:tomcat-jdbc:7.0.+'
    compile name: 'dyuproject-openid-1.1'
    compile name: 'unboundid-ldapsdk-se'
    compile name: 'jedis-2.2.5'
    compile jedisDependencies
    compile project(':src/lib')
    compile project(':src/rest-api')
    compile project(path: ':src/verkehr', configuration: 'restClient')
    testutilCompile 'org.subethamail:subethasmtp:3.1.+'
    testutilCompile project(path: ':src/lib', configuration: 'testutil')
}

task testutilJar(type: Jar) {
    baseName 'aerofs-libservlet-testutil'
    from sourceSets.testutil.output
}

artifacts {
    testutil testutilJar
    archives testutilJar
}
