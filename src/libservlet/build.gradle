//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

base {
    archivesName = 'aero-libservlet'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'gen'
        }
    }
    testutil {
        java {
            srcDir 'src/test/java'
            include 'com/aerofs/testlib/**'
            include 'com/aerofs/audit/client/MockAuditClient.java'
            include 'com/aerofs/servlets/MockSession.java'
            include 'com/aerofs/servlets/SecUtilHelper.java'
            include 'com/aerofs/servlets/lib/db/*.java'
            exclude 'com/aerofs/servlets/lib/db/Test*.java'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java {
            srcDir 'src/test/java'
            exclude 'com/aerofs/testlib/**'
            compileClasspath += testutil.output
            runtimeClasspath += testutil.output
        }
    }
}

configurations {
    testutilImplementation.extendsFrom implementation
    testutilRuntimeOnly.extendsFrom testutilImplementation

    testutil.extendsFrom testutilImplementation

    testImplementation.extendsFrom testutilImplementation
    testRuntimeOnly.extendsFrom testImplementation, testutilRuntimeOnly
}

dependencies {
    api 'org.flywaydb:flyway-core:3.2.1'
    api 'com.lambdaworks:scrypt:1.4.0'
    api 'javax.mail:mail:1.4.2'
    compileOnly 'javax.servlet:javax.servlet-api:3.0.1'
    api 'mysql:mysql-connector-java:5.1.39'
    api 'org.bouncycastle:bcprov-jdk15on:1.52'
    api 'org.bouncycastle:bcpkix-jdk15on:1.52'
    api 'org.apache.tomcat:tomcat-jdbc:7.0.61'
    api name: 'dyuproject-openid-1.1'
    api name: 'unboundid-ldapsdk-se'
    api name: 'jedis-2.2.5'
    implementation jedisDependencies
    api project(':src:lib')
    api project(':src:rest-api')
    testutilImplementation 'org.subethamail:subethasmtp:3.1.7'
    testutilImplementation project(path: ':src:lib', configuration: 'testutil')
}

task testutilJar(type: Jar) {
    archiveBaseName = 'aerofs-libservlet-testutil'
    from sourceSets.testutil.output
}

artifacts {
    testutil testutilJar
    archives testutilJar
}
