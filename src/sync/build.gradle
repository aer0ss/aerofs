//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

archivesBaseName = 'aerofs-sync'

sourceSets {
    main {
        java {
            srcDirs 'gen', 'src/main/java'
        }
    }

    testutil {
        java {
            srcDir 'src/test/java'
            include 'com/aerofs/daemon/core/lib/BaseDownloadsTest.java'
            include 'com/aerofs/daemon/core/lib/BaseStoreJoinerTest.java'
            include 'com/aerofs/daemon/core/lib/AsyncDownloadTestHelper.java'
            include 'com/aerofs/daemon/rest/BaseAbstractRestTest.java'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java {
            srcDir 'src/test/java'
            exclude 'com/aerofs/daemon/core/lib/BaseDownloadsTest.java'
            exclude 'com/aerofs/daemon/core/lib/BaseStoreJoinerTest.java'
            exclude 'com/aerofs/daemon/core/lib/AsyncDownloadTestHelper.java'
            exclude 'com/aerofs/daemon/rest/BaseAbstractRestTest.java'
            compileClasspath += testutil.output
            runtimeClasspath += testutil.output
        }
    }
}

configurations {
    testutilCompile.extendsFrom compile
    testutilRuntime.extendsFrom testutilCompile

    testutil.extendsFrom testutilCompile

    testCompile.extendsFrom testutilCompile
    testRuntime.extendsFrom(testCompile, testutilRuntime)
}

dependencies {
    compile 'org.javaswift:joss:0.9.8'
    compile 'com.amazonaws:aws-java-sdk-s3:1.9.32'
    compile project(':src/lib')
    compile project(':src/tunnel')
    compile project(':src/rest-api')
    compile project(':src/ssmp')
    compile project(path: ':src/zephyr', configuration: 'client')
    testCompile project(':src/zephyr')
    testutilCompile 'javax.mail:mail:1.4.2'
    testutilCompile 'com.jayway.restassured:rest-assured:2.8.0'
    testutilCompile 'org.hibernate:hibernate-core:4.1.9.Final'
    testutilCompile 'org.hibernate.common:hibernate-commons-annotations:4.0.1.Final'
    testutilCompile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
    testutilCompile project(':src/bifrost')
    testutilCompile project(':src/havre')
    testutilCompile project(':src/zephyr')
    testutilCompile project(path: ':src/bifrost', configuration: 'testutil')
    testutilCompile project(path: ':src/lib', configuration: 'testutil')
}

task testutilJar(type: Jar) {
    baseName 'aerofs-sync-testutil'
    from sourceSets.testutil.output
}

artifacts {
    testutil testutilJar
    archives testutilJar
}
