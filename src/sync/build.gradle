//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

archivesBaseName = 'aerofs-sync'

test {
    // Daemon tests can be parallelized safely.
    maxParallelForks = 8
}

sourceSets {
    main {
        java {
            srcDirs 'gen', 'src/main/java'
        }
    }

    testutil {
        java {
            srcDir 'src/test/java'
            include 'com/aerofs/daemon/core/lib/BaseDownloadsTest.java'
            include 'com/aerofs/daemon/core/lib/BaseStoreJoinerTest.java'
            include 'com/aerofs/daemon/core/lib/AsyncDownloadTestHelper.java'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java {
            srcDir 'src/test/java'
            exclude 'com/aerofs/daemon/core/lib/BaseDownloadsTest.java'
            exclude 'com/aerofs/daemon/core/lib/BaseStoreJoinerTest.java'
            exclude 'com/aerofs/daemon/core/lib/AsyncDownloadTestHelper.java'
            compileClasspath += testutil.output
            runtimeClasspath += testutil.output
        }
    }
}

configurations {
    testutilCompile.extendsFrom compile
    testutilRuntime.extendsFrom testutilCompile

    testutil.extendsFrom testutilCompile

    testCompile.extendsFrom testutilCompile
    testRuntime.extendsFrom(testCompile, testutilRuntime)
}

dependencies {
    compile 'org.javaswift:joss:0.9.8'
    compile 'com.amazonaws:aws-java-sdk-s3:1.9.32'
    compile project(':src/lib')
    compile project(':src/ssmp')
    compile project(path: ':src/zephyr', configuration: 'client')
    testCompile 'javax.mail:mail:1.4.2'
    testCompile project(':src/zephyr')
    testCompile project(path: ':src/lib', configuration: 'testutil')
    testutilCompile project(path:':src/lib', configuration:'testutil')
}

task testutilJar(type: Jar) {
    baseName 'aerofs-sync-testutil'
    from sourceSets.testutil.output
}

artifacts {
    testutil testutilJar
    archives testutilJar
}