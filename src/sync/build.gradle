//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

base {
    archivesName = 'aero-sync'
}

//test {
//    testLogging {
//        events "passed", "skipped", "failed", "standardOut", "standardError"
//    }
//}

sourceSets {
    main {
        java {
            srcDirs 'gen', 'src/main/java'
        }
    }

    testutil {
        java {
            srcDir 'src/test/java'
            include 'com/aerofs/testlib/**'
            include 'com/aerofs/daemon/core/lib/BaseDownloadsTest.java'
            include 'com/aerofs/daemon/core/lib/BaseStoreJoinerTest.java'
            include 'com/aerofs/daemon/core/lib/AsyncDownloadTestHelper.java'
            include 'com/aerofs/daemon/rest/BaseAbstractRestTest.java'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java {
            srcDir 'src/test/java'
            exclude 'com/aerofs/daemon/core/lib/BaseDownloadsTest.java'
            exclude 'com/aerofs/daemon/core/lib/BaseStoreJoinerTest.java'
            exclude 'com/aerofs/daemon/core/lib/AsyncDownloadTestHelper.java'
            exclude 'com/aerofs/daemon/rest/BaseAbstractRestTest.java'
            compileClasspath += testutil.output
            runtimeClasspath += testutil.output
        }
    }
}

configurations {
    testutilImplementation.extendsFrom api
    testutilImplementation.extendsFrom implementation
    testutilRuntimeOnly.extendsFrom testutilImplementation

    testutil.extendsFrom testutilImplementation

    testImplementation.extendsFrom testutilImplementation
    testRuntimeOnly.extendsFrom(testImplementation, testutilRuntimeOnly)
}

dependencies {
    implementation 'org.javaswift:joss:0.9.8'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.9.32'
    implementation name: 'guice-multibindings-aerofs'
    api project(':src:libclient')
    api project(':src:tunnel')
    api project(':src:rest-api')
    api project(':src:ssmp')
    api project(path: ':src:zephyr', configuration: 'client')
    testImplementation project(':src:zephyr')
    testutilImplementation 'javax.mail:mail:1.4.2'
    testutilImplementation 'com.jayway.restassured:rest-assured:2.8.0'
    testutilImplementation 'org.hibernate:hibernate-core:4.1.9.Final'
    testutilImplementation 'org.hibernate.common:hibernate-commons-annotations:4.0.1.Final'
    testutilImplementation 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
    testutilImplementation project(':src:bifrost')
    testutilImplementation project(':src:havre')
    testutilImplementation project(':src:zephyr')
    testutilImplementation project(path: ':src:bifrost', configuration: 'testutil')
    testutilImplementation project(path: ':src:libclient', configuration: 'testutil')
}

task testutilJar(type: Jar) {
    archiveBaseName = 'aerofs-sync-testutil'
    from sourceSets.testutil.output
}

artifacts {
    testutil testutilJar
    archives testutilJar
}
