//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

base {
    archivesName = 'aero-libclient'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'gen'
        }
    }
    testutil {
        java {
            srcDir 'src/test/java'
            include 'com/aerofs/testlib/**'
            include 'com/aerofs/defects/MockDefects.java'
            include 'com/aerofs/ritual_notification/MockRNSConfiguration.java'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java {
            srcDir 'src/test/java'
            exclude 'com/aerofs/testlib/**'
            exclude 'com/aerofs/defects/MockDefects.java'
            exclude 'com/aerofs/ritual_notification/MockRNSConfiguration.java'
            compileClasspath += testutil.output
            runtimeClasspath += testutil.output
        }
    }
}

configurations {
    testutilImplementation.extendsFrom api
    testutilImplementation.extendsFrom implementation
    testutilRuntimeOnly.extendsFrom testutilImplementation

    testutil.extendsFrom testutilImplementation

    testImplementation.extendsFrom testutilImplementation
    testRuntimeOnly.extendsFrom(testImplementation, testutilRuntimeOnly)
}

dependencies {
    api 'commons-lang:commons-lang:2.6'
    api 'com.aerofs:sqlite-jdbc:3.21.0'
    api 'com.aerofs:jnotify:0.96'
    api 'com.aerofs:aerofsd:0.3.6'
    api name: 'guice-aerofs'
    api name: 'netty-native-socket'
    api name: 'junixsocket-1.3'
    api project(':src:lib')
    testutilImplementation project(path:':src:lib', configuration:'testutil')
}

task testutilJar(type: Jar) {
    archiveBaseName = 'aerofs-lib-testutil'
    from sourceSets.testutil.output
}

artifacts {
    testutil testutilJar
    archives testutilJar
}
