//-----------------------------------------------------------------------------
// Copyright (c) Air Computing Inc., 2013.
//-----------------------------------------------------------------------------

archivesBaseName = 'aerofs-lib'

sourceSets {
    testutil {
        java {
            srcDir 'src/test/java'
            include 'com/aerofs/testlib/**'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java {
            srcDir 'src/test/java'
            exclude 'com/aerofs/testlib/**'
            compileClasspath += testutil.output
            runtimeClasspath += testutil.output
        }
    }
}

configurations {
    testutilCompile.extendsFrom compile
    testutilRuntime.extendsFrom testutilCompile

    testutil.extendsFrom testutilCompile

    testCompile.extendsFrom testutilCompile
    testRuntime.extendsFrom(testCompile, testutilRuntime)
}

dependencies {
    compile 'ch.qos.logback:logback-core:1.1.4-SNAPSHOT'
    compile 'ch.qos.logback:logback-classic:1.1.4-SNAPSHOT'
    compile 'commons-lang:commons-lang:2.6'
    compile project(':src/base')
    compile project(path:':src/aero-auth', configuration:'client')
    testutilCompile project(path:':src/base', configuration:'testutil')
}

task testutilJar(type: Jar) {
    baseName 'aerofs-lib-testutil'
    from sourceSets.testutil.output
}

artifacts {
    testutil testutilJar
    archives testutilJar
}
