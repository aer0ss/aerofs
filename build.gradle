/*
 * Copyright (c) Air Computing Inc., 2013.
 */

apply plugin: 'java'

repositories {
    mavenLocal()
    maven { url 'http://repos.arrowfs.org/nexus/content/repositories/central/' }
    maven { url 'http://repos.arrowfs.org/nexus/content/repositories/apache-snapshots/' }
    maven { url 'http://repos.arrowfs.org/nexus/content/repositories/codehaus-snapshots/' }
    maven { url 'http://repos.arrowfs.org/nexus/content/repositories/google-api-client-libraries/' }
    maven { url 'http://repos.arrowfs.org/nexus/content/repositories/thirdparty/' }
    maven { url 'http://repos.arrowfs.org/nexus/content/repositories/releases/' }
    maven { url 'http://repos.arrowfs.org/nexus/content/repositories/snapshots/' }
}

configurations {
    // N.B. these are all excluded from dynamic-config-client, but other jars
    // (e.g. synctime-client) internally use the dynamic-config-client, so the
    // exclusions must be made global
    all*.exclude module: 'findbugs'           // we don't want findbugs dependencies in production
    all*.exclude module: 'slf4j-api'          // for all configurations, exclude slf4j because we ship our own
    all*.exclude module: 'guava'              // exclude guava in preference of our own version
    all*.exclude module: 'commons-logging'    // exclude commons logging because we use slf4j
    all*.exclude module: 'log4j-over-slf4j'   // don't want multiple logging backends; we ship log4j
    all*.exclude group:  'ch.qos.logback'     // exclude logback because we use slf4j/log4j
}

/**
 * Define the dependencies for dynamic-config
 */
dependencies {
    compile('com.aerofs.dynamic-config:dynamic-config-client:0.1')
    compile('com.aerofs.synctime:synctime-client:0.9')
}

/**
 * IMPORTANT: I split the downloading and copying into two steps
 * because gradle deletes the sync directory on every run
 * FIXME (AG): understand exactly why this happens
 */

/**
 * IMPORTANT: private! do not call externally!
 * copy jars from the maven cache into the sync directory
 */
task syncDependencies(type: Sync) {
    from configurations.compile
    into "$projectDir/resource/dynamic-config"
}

/**
 * copy the dependency jars into the final location (client/common/lib)
 */
task libs(type: Copy, dependsOn: syncDependencies) {
    from "$projectDir/resource/dynamic-config"
    into "$projectDir/resource/client/common/lib"
}
